// *****
// * Original code by unknown
// * Optimized by Hanebu
// *****
//
// HOW TO USE IT:
//
// 1. Add or remove as many Tomes as you want
//    To get the ID of you tome use:
//    ">info" ingame to get a target cursor
//    then target the desired tome
//    and paste the value below.
//
removelist tomes
createlist tomes
clearsysmsg

// Set tome serials
// Add as little or as many
// different types of map
// tomes here as you want
// use >info in game to find
// the serial, and add it
// below like the two examples

// Skinning
pushlist tomes '0x400D5230'
// Ore
pushlist tomes '0x47F6774A'
// Lumber
pushlist tomes '0x47F67484'
// TMap
pushlist tomes '0x40C31DC8'
// Fishing
pushlist tomes '0x40048ED1'

//Set buttons
removelist buttons
createlist buttons
pushlist buttons '10'
pushlist buttons '11'
pushlist buttons '12'
pushlist buttons '13'
pushlist buttons '14'
pushlist buttons '15'
pushlist buttons '16'
pushlist buttons '17'
pushlist buttons '18'

foreach tome in tomes
    dclick tome
    waitforgump 'any'
    foreach button in buttons
    overhead button
        if insysmsg 'The world will save'
            overhead 'Pausing for Worldsave' 55
            pause 25000
            overhead 'Done' 55
        endif
        gumpresponse button
        wait 500
        while findtype 5356 backpack
            if findtype 5356 backpack
                gumpclose
                if not findtype 5365 backpack
                    overhead 'No spyglass in backpack. Stopping...' 33
                    stop
                else
                    dclicktype 5365 backpack
                    waitfortarget
                    targettype 5356 backpack
                    waitforgump 'any'
                    while not insysmsg 'reached the maximum'
                        gumpresponse  4
                        wait 1500
                    endwhile
                    gumpclose
                endif
                lifttype 5356 1 backpack
                drop tome 0 0 0
                wait 400
            endif
            dclick tome
            waitforgump 'any'
            gumpresponse button
            wait 1000
            gumpclose
        endwhile
        dclick tome
        waitforgump 'any'
    endfor
    gumpclose
endfor
